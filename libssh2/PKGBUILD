pkgname=libssh2
pkgver=1.8.0
pkgrel=2
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="https://www.libssh2.org/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('openssl')
makedepends=('zlib')
#provides=('libssh2.so')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)
source=("https://www.libssh2.org/download/$pkgname-$pkgver.tar.gz"{,.asc})
md5sums=('3d1147cae66e2959ea5441b183de1b1c'
         'SKIP')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/opt/arch \
      --disable-debug \
      --disable-dependency-tracking \
      --disable-silent-rules \
      --disable-examples-build \
      --with-openssl \
      --with-libz \
      --with-libssl-prefix=/opt/arch
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/opt/arch/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/opt/arch/share/licenses/$pkgname/LICENSE"
}
