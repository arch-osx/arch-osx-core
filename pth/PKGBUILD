pkgname=pth
pkgver=2.0.7
pkgrel=5
pkgdesc="The GNU Portable Threads."
arch=('x86_64')
url="http://www.gnu.org/software/pth/"
license=('LGPL')
depends=()
options=('!makeflags')
source=(ftp://ftp.gnu.org/gnu/pth/$pkgname-$pkgver.tar.gz{,.sig})
sha1sums=('9a71915c89ff2414de69fe104ae1016d513afeee'
          '8cb16dffbf15ba92bda8d08918eaf5995642fc38')
validpgpkeys=('4E23E878D41A0A88EDFCFA5A6E744ACBA9C09E30')  # Ralf Engelschall

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/opt/arch \
              --sysconfdir=/opt/arch/etc \
              --localstatedir=/opt/arch/var \
              --mandir=/opt/arch/share/man \
              --disable-static \
              --enable-shared \
              --with-pic

  # sed -i~ -e "s|awk=''|awk=/bin/awk|g" shtool # "Cannot find a reasonable Awk"

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR=${pkgdir} install
}
