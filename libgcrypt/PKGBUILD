pkgname=libgcrypt
pkgver=1.6.5
pkgrel=1
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error>=1.10-2')
options=('!emptydirs')
install=$pkgname.install
# https://www.gnupg.org/download/integrity_check.html
source=(ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha1sums=('c3a5a13e717f7b3e3895650afc1b6e0d3fe9c726'
          'SKIP')
validpgpkeys=('031EC2536E580D8EA286A9F22071B08A33BD3F06' # "NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>"
              'D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

#prepare() {
#  cd ${pkgname}-${pkgver}
#}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr/local \
    --disable-static \
    --disable-padlock-support
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
