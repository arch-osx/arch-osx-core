pkgname=openvpn
pkgver=2.3.10
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network)"
arch=(x86_64)
url="http://openvpn.net/index.php/open-source.html"
depends=('tuntap' 'openssl' 'lzo')
license=('custom')
source=(http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz
        http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz.asc)
sha256sums=('f8b0b5b92e35bbca1db1a7e6b49e04639e45634e9accd460459b40b2c99ec8f6'
            'SKIP')
validpgpkeys=('03300E11FED16F59715F9996C29D97ED198D22A3')  # Samuli Sepp√§nen

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/local/lib/openvpn\\\"" ./configure \
    --prefix=/usr/local \
    --sbindir=/usr/local/bin \
    --mandir=/usr/local/share/man \
    --disable-debug \
    --disable-dependency-tracking \
    --disable-silent-rules \
    --with-crypto-library=openssl \
    --enable-password-save
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  # Install openvpn
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}"/usr/local/etc/openvpn

  # Install examples
  install -d -m755 "${pkgdir}"/usr/local/share/openvpn
  sed -i -e 's#/etc/openvpn#/usr/local/etc/openvpn#g' "sample/sample-config-files/openvpn-startup.sh"
  cp -r sample/sample-config-files "${pkgdir}"/usr/local/share/openvpn/examples

  # Install license
  install -d -m755 "${pkgdir}"/usr/local/share/licenses/${pkgname}/
  ln -sf /usr/local/share/doc/${pkgname}/{COPYING,COPYRIGHT.GPL} "${pkgdir}"/usr/local/share/licenses/${pkgname}/

  # Install contrib
  install -d -m755 "${pkgdir}"/usr/local/share/openvpn/contrib
  cp -r contrib "${pkgdir}"/usr/local/share/openvpn

  # We don't use PolarSSL, so this file is unnecessary and somewhat confusing.
  rm "$pkgdir"/usr/local/share/doc/openvpn/README.polarssl
}
