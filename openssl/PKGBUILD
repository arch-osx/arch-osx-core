pkgname=openssl
_ver=1.0.2g
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=()
optdepends=('ca-certificates')
options=('!makeflags')
backup=('usr/local/etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz"
        "https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz.asc"
        'no-rpath.patch'
        'ssl3-test-failure.patch'
        'ca-dir.patch')
md5sums=('f3c710c045cdee5fd114feb69feba7aa'
         'SKIP'
         'dc78d3d06baffc16217519242ce92478'
         '62fc492252edd3283871632bb77fadbe'
         '1009ebb8a1a4b996c963dad1ad32a271')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491')

prepare() {
    cd $srcdir/$pkgname-$_ver

    # remove rpath: http://bugs.archlinux.org/task/14367
    patch -p0 -i $srcdir/no-rpath.patch

    # disable a test that fails when ssl3 is disabled
    patch -p1 -i $srcdir/ssl3-test-failure.patch

    # set ca dir to /etc/ssl by default
    patch -p0 -i $srcdir/ca-dir.patch
}

build() {
    cd $srcdir/$pkgname-$_ver

    perl ./Configure --prefix=/usr/local no-ssl2 zlib-dynamic shared enable-cms darwin64-x86_64-cc enable-ec_nistp_64_gcc_128
    #./Configure --prefix=/usr/local --openssldir=/usr/local/etc/ssl --libdir=/usr/local/lib \
    #    shared no-ssl3-method ${optflags} \
    #    "${openssltarget}" \
    #    "-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

    make depend
    make
}

check() {
    cd $srcdir/$pkgname-$_ver
    # the test fails due to missing write permissions in /etc/ssl
    # revert this patch for make test
    patch -p0 -R -i $srcdir/ca-dir.patch
    make test
    patch -p0 -i $srcdir/ca-dir.patch
}

package() {
    cd $srcdir/$pkgname-$_ver
    make INSTALL_PREFIX=$pkgdir MANDIR=/usr/local/share/man MANSUFFIX=ssl install
    install -d -m755 $pkgdir/usr/local/share/licenses/$pkgname
    install -m644 LICENSE $pkgdir/usr/local/share/licenses/$pkgname/LICENSE
}
