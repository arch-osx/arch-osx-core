pkgname=autoconf
pkgver=2.69
pkgrel=1
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="http://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=()
checkdepends=('gcc-fortran')
install=autoconf.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('50f97f4159805e374639a73e2636f22e'
         '2941e2fe687e05b3431eac6b714053bc')
validpgpkeys=('71C2CC22B1C4602927D2F3AAA7A16B4A2527436A')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  perl -pi -e 's/libtoolize/glibtoolize/' bin/autoreconf.in
  perl -pi -e 's/libtoolize/glibtoolize/' man/autoreconf.1
  ./configure --prefix=/opt/arch
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # license exception
  install -d -m755 $pkgdir/opt/arch/share/licenses/autoconf
  install -m644 COPYING.EXCEPTION \
    $pkgdir/opt/arch/share/licenses/autoconf/COPYING.EXCEPTION

  # remove unwanted file
  rm -f ${pkgdir}/opt/arch/share/info/standards.info
}
