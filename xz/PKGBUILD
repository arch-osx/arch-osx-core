pkgname=xz
pkgver=5.2.2
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('i686' 'x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=()
source=("http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz.sig")
md5sums=('7cf6a8544a7dae8e8106fdf7addfa28c'
         'SKIP')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure --prefix=/usr/local --disable-debug --disable-dependency-tracking --disable-silent-rules
    make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make check
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    install -d -m755 ${pkgdir}/usr/local/share/licenses/xz/
    #ln -sf /usr/local/share/doc/xz/COPYING ${pkgdir}/usr/local/share/licenses/xz/
    #ln -sf /usr/local/share/licenses/common/GPL2/license.txt ${pkgdir}/usr/local/share/doc/xz/COPYING.GPLv2
}
